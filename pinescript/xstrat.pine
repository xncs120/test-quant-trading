// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © xsheng120

//@version=6
indicator(title = "xtrat", shorttitle = "xtrat", overlay=true)

// ========== gex ==========
// gex_GROUP = "Gamma Exposure Settings"
// gex_show = input(false, title = "Show Gamma Exposure", tooltip = "Go to https://www.barchart.com/ (Gamma Exposure & Expected Move) for data", group = gex_GROUP, display = display.none)
// gex_flip = input.float(0, "Gamma Flip Price Level")
// gex_price_c1 = input.float(0, "Call Price Level Highest")
// gex_price_p1 = input.float(0, "Put Price Level Lowest")
// gex_expected_move1 = input.float(0, "Expected Move Price Level High")
// gex_expected_move2 = input.float(0, "Expected Move Price Level Low")

// get_level(val) => gex_show and val != 0 ? val : na

// hline(get_level(gex_flip), "Gamma Flip Price Level", color = color.gray, linestyle = hline.style_dashed, linewidth = 2, display = gex_show ? display.all : display.none, editable = gex_show)
// hline(get_level(gex_price_c1), "Call Price Level Highest", color = color.yellow, linestyle = hline.style_dashed, linewidth = 2, display = gex_show ? display.all : display.none, editable = gex_show)
// hline(get_level(gex_price_p1), "Put Price Level Lowest", color = color.orange, linestyle = hline.style_dashed, linewidth = 2, display = gex_show ? display.all : display.none, editable = gex_show)
// gex_em_1 = hline(get_level(gex_expected_move1), "Expected Move High", display = display.none, editable = false)
// gex_em_2 = hline(get_level(gex_expected_move2), "Expected Move Low", display = display.none, editable = false)
// fill(gex_em_1, gex_em_2, title = "Expected Move", color = color.rgb(44, 109, 230, 80), display = gex_show ? display.all : display.none, editable = gex_show)

// ========== sma ==========
sma_GROUP = "SMA Settings"
sma1_show = input(false, title = "", inline = "sma1", group = sma_GROUP, display = display.none)
sma1_length = input.int(defval = 20, title = "SMA1 Length", inline = "sma1", group = sma_GROUP, display = display.none)
sma2_show = input(false, title = "", inline = "sma2", group = sma_GROUP, display = display.none)
sma2_length = input.int(defval = 50, title = "SMA2 Length", inline = "sma2", group = sma_GROUP, display = display.none)
sma3_show = input(true, title = "", inline = "sma3", group = sma_GROUP, display = display.none)
sma3_length = input.int(defval = 200, title = "SMA3 Length", inline = "sma3", group = sma_GROUP, display = display.none)
sma_src = input(title = "Source", defval = hlc3, group = sma_GROUP, display = display.none)

sma1_value = ta.sma(sma_src, sma1_length)
sma2_value = ta.sma(sma_src, sma2_length)
sma3_value = ta.sma(sma_src, sma3_length)

plot(sma1_value, title = "SMA1", color = color.aqua, style = plot.style_circles, display = sma1_show ? display.all : display.none, editable = sma1_show)
plot(sma2_value, title = "SMA2", color = color.blue, style = plot.style_circles, display = sma2_show ? display.all : display.none, editable = sma2_show)
plot(sma3_value, title = "SMA3", color = color.purple, style = plot.style_circles, display = sma3_show ? display.all : display.none, editable = sma3_show)

// ========== ema ==========
ema_GROUP = "EMA Settings"
ema1_show = input(false, title = "", inline = "ema1", group = ema_GROUP, display = display.none)
ema1_length = input.int(defval = 9, title = "EMA1 Length", inline = "ema1", group = ema_GROUP, display = display.none)
ema2_show = input(false, title = "", inline = "ema2", group = ema_GROUP, display = display.none)
ema2_length = input.int(defval = 20, title = "EMA2 Length", inline = "ema2", group = ema_GROUP, display = display.none)
ema3_show = input(true, title = "", inline = "ema3", group = ema_GROUP, display = display.none)
ema3_length = input.int(defval = 200, title = "EMA3 Length", inline = "ema3", group = ema_GROUP, display = display.none)
ema_src = input(title = "Source", defval = hlc3, group = ema_GROUP, display = display.none)

ema1_value = ta.ema(ema_src, ema1_length)
ema2_value = ta.ema(ema_src, ema2_length)
ema3_value = ta.ema(ema_src, ema3_length)

plot(ema1_value, title = "EMA1", color = color.aqua, display = ema1_show ? display.all : display.none, editable = ema1_show)
plot(ema2_value, title = "EMA2", color = color.blue, display = ema2_show ? display.all : display.none, editable = ema2_show)
plot(ema3_value, title = "EMA3", color = color.purple, display = ema3_show ? display.all : display.none, editable = ema3_show)

// ========== hma ==========
hma_GROUP = "HMA Settings"
hma_show = input(false, title = "", inline = "hma", group = hma_GROUP, display = display.none)
hma_length = input.int(defval = 30, title = "HMA Length", inline = "hma", group = hma_GROUP, display = display.none)
hma_src = input(title = "Source", defval = close, group = hma_GROUP, display = display.none)

wma_n2_value = 2 * ta.wma(hma_src, math.round(hma_length/2))
wma_value = ta.wma(hma_src, hma_length)
wma_diff = wma_n2_value - wma_value
wma_sqn = math.round(math.sqrt(hma_length))

wma2_n2_value = 2 * ta.wma(hma_src[1], math.round(hma_length/2))
wma2_value = ta.wma(hma_src[1], hma_length)
wma2_diff = wma_n2_value - wma2_value
wma2_sqn = math.round(math.sqrt(hma_length))

hma_value = ta.wma(wma_diff, wma_sqn)
hma_neg_value = ta.wma(wma2_diff, wma2_sqn)

plot(hma_value, color = hma_value > hma_neg_value ? color.rgb(242, 54, 70) : color.rgb(8, 153, 129), display = hma_show ? display.all : display.none, editable = hma_show)

// ========== vwap ==========
vwap_GROUP = "VWAP Settings"
vwap_tf_hide = input(true, title = "Hide VWAP on 30min or above", group = vwap_GROUP, display = display.none)
vwap_show = input(true, title = "", inline = "vwap", group = vwap_GROUP, display = display.none)
vwap_anchor = input.string(defval = "Session", title = "Anchor Period", inline = "vwap", options = ["Session", "Week", "Month", "Quarter", "Year", "Decade", "Century", "Earnings", "Dividends", "Splits"], group = vwap_GROUP, display = display.none)
vwap_src = input(title = "Source", defval = hlc3, group = vwap_GROUP, display = display.none)
vwap_offset = input.int(0, title = "Offset", group = vwap_GROUP, minval = 0, display = display.none)

cum_volume = ta.cum(volume)
if barstate.islast and cum_volume == 0
    runtime.error("No volume is provided by the data vendor.")

isNewPeriod = switch vwap_anchor
	"Earnings"  => not na(request.earnings(syminfo.tickerid, earnings.actual, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true))
	"Dividends" => not na(request.dividends(syminfo.tickerid, dividends.gross, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true))
	"Splits"    => not na(request.splits(syminfo.tickerid, splits.denominator, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true))
	"Session"   => timeframe.change("D")
	"Week"      => timeframe.change("W")
	"Month"     => timeframe.change("M")
	"Quarter"   => timeframe.change("3M")
	"Year"      => timeframe.change("12M")
	"Decade"    => timeframe.change("12M") and year % 10 == 0
	"Century"   => timeframe.change("12M") and year % 100 == 0
	=> false
isEsdAnchor = vwap_anchor == "Earnings" or vwap_anchor == "Dividends" or vwap_anchor == "Splits"
if na(vwap_src[1]) and not isEsdAnchor
	isNewPeriod := true

float vwap_value = na

if not (vwap_tf_hide and not (timeframe.in_seconds() < (30 * 60)))
    [_vwap, _stdevUpper, _] = ta.vwap(vwap_src, isNewPeriod, 1)
	vwap_value := _vwap

plot(vwap_value, title = "VWAP", color = color.white, offset = vwap_offset, display = vwap_show ? display.all : display.none, editable = vwap_show)

// ========== pivot high/low ==========
phl_GROUP = "Pivot High/Low Settings"
phl_show = input(false, title = "Show Pivot High/Low", group = phl_GROUP, display = display.none)
phl_left_length  = input.int(5, "Pivot Left Length", group = phl_GROUP)
phl_right_length = input.int(5, "Pivot Right Length", group = phl_GROUP)

phl_high = ta.pivothigh(high, phl_left_length, phl_right_length)
phl_low  = ta.pivotlow(low, phl_left_length, phl_right_length)

var float phl_last_high = na
var float phl_prev_high = na
var float phl_last_low  = na
var float phl_prev_low  = na

if not na(phl_high)
    phl_prev_high := phl_last_high
    phl_last_high := phl_high

if not na(phl_low)
    phl_prev_low := phl_last_low
    phl_last_low := phl_low

isLH = not na(phl_high) and phl_high < phl_prev_high
isHL = not na(phl_low)  and phl_low  > phl_prev_low

// Plot labels on the chart
plotshape(isLH, "LH", shape.labeldown, location.abovebar, color.red,   text="LH", textcolor = color.white, offset=-phl_right_length, display = phl_show ? display.all : display.none, editable = phl_show)
plotshape(isHL, "HL", shape.labelup,   location.belowbar, color.green, text="HL", textcolor = color.white, offset=-phl_right_length, display = phl_show ? display.all : display.none, editable = phl_show)

// ========== atr sl ==========
atrsl_GROUP = "ATR SL Settings"
atrsl_show = input(false, title = "Show ATR SL", group = atrsl_GROUP)
atrsl_length = input.int(defval = 14, title = "ATR SL Length", minval = 1, group = atrsl_GROUP)
atrsl_smoothing = input.string(defval = "RMA", title = "Smoothing", options = ["RMA", "SMA", "EMA", "WMA"], group = atrsl_GROUP)
atrsl_multiplier = input.float(defval = 1.5, title = "Multiplier", minval = 1, group = atrsl_GROUP)

atr_ma_function(source, length) =>
	switch atrsl_smoothing
		"RMA" => ta.rma(source, length)
		"SMA" => ta.sma(source, length)
		"EMA" => ta.ema(source, length)
		=> ta.wma(source, length)

atrsl_value = atr_ma_function(ta.tr(true), atrsl_length) * atrsl_multiplier
plot(hlc3 - atrsl_value, title = "Long Position ATR SL", color = color.yellow, style = plot.style_circles, display = atrsl_show ? display.all : display.none, editable = atrsl_show)
plot(hlc3 + atrsl_value, title = "Short Position ATR SL", color = color.yellow, style = plot.style_circles, display = atrsl_show ? display.all : display.none, editable = atrsl_show)

// ========== session ==========
session_GROUP = "Session settings"
session_show = input(false, title = "Show Session First Candle High Low", group = session_GROUP, display = display.none)
session_capture = input(5, title= "Capture Session Time (minutes)", group = session_GROUP)
session_starttime = input.session("0930-0935", title = "Session StartTime", group = session_GROUP)

session_time = time(timeframe.period, session_starttime + ":1234567")
session_tf_hide = not (timeframe.in_seconds() < (30 * 60))
in_session = not na(session_time)
is_first = in_session and not in_session[1]

session_high = float(na)
session_low = float(na)

if is_first
    session_high := high
    session_low := low
else
    session_high := session_high[1]
    session_low := session_low[1]

if high > session_high and in_session
    session_high := high
if low < session_low and in_session
    session_low := low

plot(session_tf_hide ? na : session_high, color = session_high[1] != session_high ? na : color.green, title = "Session High", display = session_show ? display.all : display.none, editable = session_show)
plot(session_tf_hide ? na : session_low, color = session_low[1] != session_low ? na : color.red, title = "Session Low", display = session_show ? display.all : display.none, editable = session_show)

// ========== heikin ashi ==========
heikin_GROUP = "Heikin Ashi settings"
heikin_show = input(false, title = "Show Heikin Ashi with Doji indicator", group = heikin_GROUP, display = display.none)

var float ha_open = na
ha_close = (open + high + low + close) / 4
ha_open  := na(ha_open[1]) ? (open + close) / 2 : (ha_open[1] + ha_close[1]) / 2
ha_high  = math.max(high, math.max(ha_open, ha_close))
ha_low   = math.min(low, math.min(ha_open, ha_close))
plotcandle(ha_open, ha_high, ha_low, ha_close, title = "Heikin Ashi", color = ha_close >= ha_open ? color.rgb(8, 153, 129) : color.rgb(242, 54, 70), wickcolor = ha_close >= ha_open ? color.rgb(8, 153, 129) : color.rgb(242, 54, 70), display = heikin_show ? display.all : display.none, editable = heikin_show)
doji = math.abs(ha_close - ha_open) <= (ha_high - ha_low) * 0.1
plotshape(doji, title = "Doji Arrow", style = shape.xcross, location = location.abovebar, color = color.rgb(255, 235, 59), size = size.tiny, text = "", display = heikin_show ? display.all : display.none, editable = heikin_show)

// ========== low volatility range breakout ==========
lvr_GROUP = "LV Range Breakout Settings"
lvr_show = input(false, title = "Show LV Range Breakout", group = lvr_GROUP, display = display.none)
int lvr_length = input(50, "Volatility Length")
int lvr_volatility_level = input.int(10, "Volatility Level", minval = 5, maxval = 20, tooltip = "Sensitivity of Volatility")
float lvr_box_high = input.float(2., "Boxes High", step = 0.1)

if lvr_show
    color lvr_up = #23bdad
    color lvr_dn = #b82372
    float lvr_h = ta.highest(lvr_length)
    float lvr_l = ta.lowest(lvr_length)
    float lvr_atr = ta.atr(200)*lvr_box_high
    var lvr_count_activity = int(na)
    var lvr_box = box(na)
    var lvr_line = line(na)
    var lvr_prob_up = int(na)
    var lvr_prob_dn = int(na)
    var lvr_check = bool(na)
    bool lvr_lv_condition = ta.change(lvr_l[1] == lvr_l) or ta.change(lvr_h[1] == lvr_h)

    if not lvr_lv_condition
        lvr_count_activity += 1

    if lvr_lv_condition
        lvr_count_activity := 0

    if bar_index == 201
        lvr_check := true

    if (lvr_count_activity == lvr_volatility_level) and lvr_check
        lvr_box  := box.new(bar_index-lvr_volatility_level, high + lvr_atr, bar_index + 40, low - lvr_atr,
                            bgcolor = na, 
                            border_color = chart.fg_color)
        half   = (lvr_box.get_bottom() + lvr_box.get_top()) / 2

        lvr_line := line.new(bar_index-lvr_volatility_level, half, bar_index+40, half, color = chart.fg_color)
        lvr_check := false

    if hl2 > lvr_line.get_y1()
        lvr_prob_up += 1

    if hl2 < lvr_line.get_y1()
        lvr_prob_dn += 1

    if ta.crossunder(close, lvr_box.get_bottom())
        lvr_prob_up := 0
        lvr_prob_dn := 0
        lvr_check   := true

        label.new(bar_index, lvr_box.get_top(), "Break Dn", xloc.bar_index, yloc.price, #00000000, 
                    label.style_label_down, chart.fg_color)
        lvr_box.set_right(bar_index)
        lvr_box.set_border_style(line.style_dashed)
        lvr_box.set_border_color(lvr_dn)
        line.delete(lvr_line[1])

        lvr_box := na

    if  ta.crossover(close, lvr_box.get_top())
        lvr_prob_up := 0
        lvr_prob_dn := 0
        lvr_check   := true

        label.new(bar_index, lvr_box.get_bottom(), "Break Up", xloc.bar_index, yloc.price, #00000000, 
                    label.style_label_up, chart.fg_color)

        lvr_box.set_right(bar_index)
        lvr_box.set_border_style(line.style_dashed)
        lvr_box.set_border_color(lvr_up)
        line.delete(lvr_line[1])

        lvr_box := na

    if barstate.islast
        label.delete(label.new(bar_index+40, lvr_box.get_bottom(), str.tostring(lvr_prob_dn), 
                    xloc.bar_index, 
                    yloc.price, 
                    #00000000, 
                    label.style_label_up,  
                    lvr_prob_dn > lvr_prob_up ? lvr_dn : chart.fg_color)[1])

        label.delete(label.new(bar_index+40, lvr_box.get_top(), str.tostring(lvr_prob_up), 
                    xloc.bar_index,
                    yloc.price, 
                    #00000000, 
                    label.style_label_down, 
                    lvr_prob_dn < lvr_prob_up ? lvr_up : chart.fg_color)[1])

    lvr_box.set_right(bar_index+40)
    lvr_line.set_x2(bar_index+40)
